plugins {
    id 'java-library'
    id 'checkstyle'
    id 'maven-publish'
    id 'org.sonarqube' version '3.3'
}

group = 'template'
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.aventstack', name: 'extentreports', version: '5.0.8'
    implementation group: 'com.aventstack', name: 'extentreports-testng-adapter', version: '1.2.2'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.29'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
    implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
    implementation group: 'org.testng', name: 'testng', version: '7.4.0'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    implementation group: 'org.jetbrains', name: 'annotations', version: '22.0.0'
    implementation group: 'com.neovisionaries', name: 'nv-i18n', version:'1.29'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.5'
    implementation group: 'com.konghq', name: 'unirest-java', version: '3.12.0'
}

test {
    useTestNG() {
        useDefaultListeners = true
        systemProperties System.getProperties()
        def groupsToInclude = System.getProperty("groups", "full-regression")
        def groupsToExclude = System.getProperty("excludeGroups", "foo")
        def threads = System.getProperty("threads", "1").toInteger()
        includeGroups groupsToInclude
        excludeGroups groupsToExclude
        options {
            parallel = 'methods'
            threadCount = threads
        }
    }

    // turn off Gradle's HTML report to avoid replacing the
    // reports generated by TestNG library
    reports.html.enabled = false
    testLogging.showStandardStreams = true
}

sonarqube {
    properties {
        property "sonar.projectKey", "barranquerox_template-framework"
        property "sonar.organization", "barranquerox"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

checkstyle {
    configFile file("config/checkstyle/checkstyle.xml")
    reportsDir = file("$project.buildDir/reports/checkstyle/")
    ignoreFailures = false
    maxWarnings = 0
}

checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled true
    }
}